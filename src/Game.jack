class Game {
    field boolean isRun;
    field Map map;
    field Player player;
    field Array enemies;
    field int countEnemies;

    constructor Game new() {
        let map = Map.new();
        let player = Player.new(map);
        let countEnemies = 1;
        let enemies = Array.new(countEnemies);
        let enemies[0] = Enemy.new(Vector2.new(1, 9), map);
        return this;
    }

    method void start() {
        let isRun = true;
        while (isRun) {
            do readKeyboard();
            do moveEnemies();
            do Screen.clearScreen();
            do map.display();
            do player.display();
            do displayEnemies();
            do Sys.wait(200);
        }
        return;
    }

    method void displayEnemies() {
        var Enemy curEnemy;
        var int i;
        let i = 0;
        while (i < countEnemies) {
            let curEnemy = enemies[i];
            do curEnemy.display();
            let i = i + 1;
        }
        return;
    }

    method void moveEnemies() {
        var Enemy curEnemy;
        var int i;
        let i = 0;
        while (i < countEnemies) {
            let curEnemy = enemies[i];
            do curEnemy.move();
            let i = i + 1;
        }
        return;
    }

    method void readKeyboard() {
        var char key;
        var Vector2 moveDirection;
        let key = Keyboard.keyPressed();
        let moveDirection = Vector2.zero();

        if (key = Settings.getKeyLeft()) {
            let moveDirection = Vector2.left();
        }
        if (key = Settings.getKeyRight()) {
            let moveDirection = Vector2.right();
        }
        if (key = Settings.getKeyUp()) {
            let moveDirection = Vector2.up();
        }
        if (key = Settings.getKeyDown()) {
            let moveDirection = Vector2.down();
        }
        do player.move(moveDirection);
        do moveDirection.dispose();
        return;
    }
}
