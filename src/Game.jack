class Game {
    field boolean isRun;
    field Map map;
    field Player player;
    field Array enemies;
    field int countEnemies;

    constructor Game new() {
        var Enemy enemy1, enemy2, enemy3;
        let map = Map.new();
        let player = Player.new(map);

        

        let countEnemies = 3;
        let enemy1 = Enemy.new(Vector2.new(13, 1), map);
        let enemy2 = Enemy.new(Vector2.new(1, 13), map);
        let enemy3 = Enemy.new(Vector2.new(13, 13), map);

        let enemies = Array.new(countEnemies);
        let enemies[0] = enemy1;
        let enemies[1] = enemy2;
        let enemies[2] = enemy3;


        do map.addNoEmptyCell(player.getCoord());
        do map.addNoEmptyCell(enemy1.getCoord());
        do map.addNoEmptyCell(enemy2.getCoord());
        do map.addNoEmptyCell(enemy3.getCoord());
        return this;
    }

    method void start() {
        let isRun = true;
        while (isRun) {
            do readKeyboard();
            do moveEnemies();

            do map.display();
            do player.display();
            do displayEnemies();
            do Sys.wait(200);
        }
        return;
    }

    method void displayEnemies() {
        var Enemy curEnemy;
        var int i;
        let i = 0;
        while (i < countEnemies) {
            let curEnemy = enemies[i];
            do curEnemy.display();
            let i = i + 1;
        }
        return;
    }

    method void moveEnemies() {
        var Enemy curEnemy;
        var int i;
        let i = 0;
        while (i < countEnemies) {
            let curEnemy = enemies[i];
            do curEnemy.move();
            let i = i + 1;
        }
        return;
    }

    method void readKeyboard() {
        var char key;
        var Vector2 moveDirection;
        let key = Keyboard.keyPressed();
        let moveDirection = Vector2.zeroReadOnly();

        if (key = Settings.getKeyLeft()) {
            let moveDirection = Vector2.leftReadOnly();
        }
        if (key = Settings.getKeyRight()) {
            let moveDirection = Vector2.rightReadOnly();
        }
        if (key = Settings.getKeyUp()) {
            let moveDirection = Vector2.upReadOnly();
        }
        if (key = Settings.getKeyDown()) {
            let moveDirection = Vector2.downReadOnly();
        }
        do player.move(moveDirection);
        return;
    }
}
